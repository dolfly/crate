apply plugin: 'java'

archivesBaseName = 'crate-testing'

configurations {
    all*.exclude group: 'org.elasticsearch'
    ideaConf
}


idea {
    module {
        // ensure es dependencies are available in intellij as well
        def esProject = parent.project.subprojects.find({it.name == 'es'})
        scopes.COMPILE.plus += [configurations.ideaConf]
        scopes.COMPILE.plus.add(0, esProject.configurations.lucenetest)
        scopes.COMPILE.plus.add(0, esProject.configurations.testOutput)
    }
}
evaluationDependsOn(':es')

dependencies {
    compile ("org.mockito:mockito-core:1.10.8") {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    // es test classes
    compile project(path: ':es', configuration: 'testOutput')

    // idea cannot handle project dependencies with different configurations
    // so we have to declare the compiled test sources here
    // and add them above so idea sees them
    ideaConf project(':es').sourceSets.test.output

    // main dependencies and es.jar
    compile project(':es')

}

test {
    testLogging.exceptionFormat = 'full'
    jacoco.enabled = false
}
